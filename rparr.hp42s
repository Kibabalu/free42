@ R||R: Two resistors in parallel
@
@ R1    1st restor in [Ohm]
@ R2    2nd resistor in [Ohm]
@ R     resulting resitor in [Ohm]
@ 
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@ 2021-04-10: Initial version
@
00 { 94-Byte Prgm }
01 LBL "R||R"
02 MVAR "R1"
03 MVAR "R2"
04 MVAR "R"
05 VARMENU "R||R"

06 CF 21                @ continue program execution after a VIEW instruction
07 REALRES              @ calculate real results only
08 STOP
09 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
10 PGMSLV ".R||R"       @ specify solver routine
11 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
12 VIEW IND "CONTROL"   @ View the solution 
13 GTO "R||R"           @ return to start

14 LBL ".R||R"
15 RCL "R1"
16 1/X
17 RCL "R2"
18 1/X
19 +
20 1/X
21 RCL- "R"

22 END
