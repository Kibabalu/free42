@ computation of a Pi Coupler resp. Collins Filter
@
@ Input:
@ resonant frequency f0 in [Hz]
@ source impedance Z1 in [Ohm]
@ load impedance Z2 in [Ohm]
@ resonator Q factor (if set to 0 a default value of 15 is used 15)
@
@ Output:
@ capacitance C1 in [F]
@ capacitance C2 in [F]
@ inductance L in [H]
@
@ 01.03.2022 Frank Kirschbaum, frank.kirschbaumr@gmail.com
@
00 { 150-Byte Prgm }
01 LBL "PiCplr"

02 INPUT "f0"
03 INPUT "Z1"
04 INPUT "Z2"
05 MVAR "Q"
06 15
07 STO "Q"
08 INPUT "Q"

09 RCL "Z1"
10 RCL× "Z2"
11 SQRT
12 RCL+ "Z1"
13 RCL× "f0"
14 2
15 ×
16 PI
17 ×
18 1/X
19 RCL× "Q"
20 2
21 ×
22 STO "C1"
23 VIEW "C1"
24 STOP

25 RCL "Z1"
26 RCL× "Z2"
27 SQRT
28 RCL+ "Z2"
29 RCL× "f0"
30 2
31 ×
32 PI
33 ×
34 1/X
35 RCL× "Q"
36 2
37 ×
38 STO "C2"
39 VIEW "C2"
40 STOP

41 RCL "Z1"
42 RCL× "Z2"
43 SQRT
44 2
45 ×
46 RCL+ "Z2"
47 RCL+ "Z1"
48 RCL÷ "Q"
49 RCL÷ "f0"
50 PI
51 ÷
52 4
53 ÷
54 STO "L"
55 VIEW "L"
56 STOP
57 RTN
