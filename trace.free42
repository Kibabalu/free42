@ computation of PCB traces according IPC-2221 standard
@
@ internernal traces : I = 0.024 x dT^0.44 x A^0.725
@ externernal traces: I = 0.048 x dT^0.44 x A^0.725
@
@ The formulas are defined for the units A, K and mil. The programm
@ takes and gives the metric unit m instead of mil!
@
@ Default value for the trace height is 35um
@
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@ 2022-02-08: Initial version
@
00 { 427-Byte Prgm }
01 LBL "Trace"

02 CLMENU
03 "extern"                @ main Menu: choice between internernal and externernal traces
04 KEY 1 XEQ "extern"
05 "intern"
06 KEY 2 XEQ "intern"
07 MENU
08 STOP

09 LBL "extern"            @ routine for externernal traces
10 MVAR "IMax"
11 MVAR "dT"
12 MVAR "width"
13 MVAR "height"
14 VARMENU "extern"

15 CF 21                @ continue program execution after a VIEW instruction
16 STOP
17 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
18 PGMSLV ".extern"        @ specify solver routine
19 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
20 VIEW IND "CONTROL"   @ View the solution
21 GTO "extern"            @ return to start

22 LBL ".extern"           @ function to be solved for externernal traces
23 XEQ 00               @ call subroutine for default values
24 RCL "width"
25 39370.07874
26 ×
27 RCL× "height"
28 39370.07874
29 ×
30 0.725
31 Y↑X
32 RCL "dT"
33 0.44
34 Y↑X
35 ×
36 0.048
37 ×
38 RCL- "IMax"
39 RTN

40 LBL "intern"            @ routine for internernal traces
41 MVAR "IMax"
42 MVAR "dT"
43 MVAR "width"
44 MVAR "height"
45 VARMENU "intern"

46 CF 21                @ continue program execution after a VIEW instruction
47 STOP
48 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
49 PGMSLV ".intern"        @ specify solver routine
50 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
51 VIEW IND "CONTROL"   @ View the solution
52 GTO "intern"            @ return to start

53 LBL ".intern"           @ function to be solved for internernal traces
54 XEQ 00               @ call subroutine for default values
55 RCL "width"
56 39370.07874
57 ×
58 RCL× "height"
59 39370.07874
60 ×
61 0.725
62 Y↑X
63 RCL "dT"
64 0.44
65 Y↑X
66 ×
67 0.024
68 ×
69 RCL- "IMax"
70 RTN

71 LBL 00               @ subroutine for default values
72 RCL "height"
73 X≤0?
74 0.35ᴇ-6              @ default track height of 35um
75 STO "height"
76 RTN

77 END
