@ m2Vrms: dBm to VRMS Converter and vice versa
@ (Solver function)
@
@ converting dBm to Vrms
@
@ V = 10^((dBm-30)/20)*sqrt(Z0)
@
@ Frank Kirschbaum, frank.kirschbaum@gmail.com
@ 2021-04-10: Initial version
@
00 { 137-Byte Prgm }
01 LBL "m2Vrms"
02 MVAR "dBm"
03 MVAR "Vrms"
04 MVAR "Z0"
05 VARMENU "m2Vrms"

06 CF 21 @ continue program execution after a VIEW instruction
07 REALRES @ calculate real results only
08 STOP
09 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
10 PGMSLV ".m2Vrms"     @ specify solver routine
11 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
12 VIEW IND "CONTROL"   @ View the solution
13 GTO "m2Vrms"         @ return to start

14 LBL ".m2Vrms"        @ function to solve
15 XEQ 00               @ default values
16 RCL "dBm"
17 30
18 -
19 20
20 ÷
21 10↑X
22 RCL "Z0"
23 SQRT
24 ×
25 RCL- "Vrms"
26 RTN
27 LBL 00 @ default values
28 RCL "Z0"
29 X≤0?
30 50 @ default for Z0
31 STO "Z0"
32 RTN

33 END
