@ m2Vrms: dBm to VRMS Converter and vice versa
@ (Solver function)
@
@ converting dBm to Vrms
@
@ V = 10^((dBm-30)/20)*sqrt(Z0)
@
@ Frank Kirschbaum, frank.kirschbaum@gmail.com
@ 2021-04-10: Initial version
@ 2021-04-16: Final version
@
00 { 137-Byte Prgm }
01 LBL "m2Vrms"
02 MVAR "dBm"
03 MVAR "Vrms"
04 MVAR "Z0"
05 VARMENU "m2Vrms"

06 CF 21 @ continue program execution after a VIEW instruction
07 STOP
08 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
09 PGMSLV ".m2Vrms"     @ specify solver routine
10 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
11 VIEW IND "CONTROL"   @ View the solution
12 GTO "m2Vrms"         @ return to start

13 LBL ".m2Vrms"        @ function to solve
14 XEQ 00 @ call subroutine for default values
15 RCL "dBm"
16 30
17 -
18 20
19 ÷
20 10↑X
21 RCL "Z0"
22 SQRT
23 ×
24 RCL- "Vrms"
25 RTN
26 LBL 00 @ subroutine for default values
27 RCL "Z0"
28 X≤0?
29 50 @ default for Z0
30 STO "Z0"
31 RTN

32 END
