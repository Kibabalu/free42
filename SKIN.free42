@ SKIN: Skin effect computations
@
@ µr relative permeability in H/m
@ f frequency in Hz
@ rho = spec. resistivity in S/m
@ δelta skin depth in m
@
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@
@ 2021-04-15: Initial version
@ 2021-04-16: final version
@
00 { 155-Byte Prgm }
01 LBL "SKIN"
02 MVAR "µr"
03 MVAR "f"
04 MVAR "rho"
05 MVAR "delta"
06 VARMENU "SKIN"

07 CF 21                @ continue program execution after a VIEW instruction
08 REALRES              @ calculate real results only
09 STOP
10 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
11 PGMSLV ".SKIN"       @ specify solver routine
12 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
13 VIEW IND "CONTROL"   @ View the solution
14 GTO "SKIN"           @ return to start

15 LBL ".SKIN"          @ function to solve
16 RCL "µr"             
17 X≤0?
18 0.999991             @ default for copper
19 STO "µr"
20 RCL "rho"            
21 X≤0?
22 1.678ᴇ-8             @ default for copper
23 STO "rho"
24 PI
25 RCL× "f"
26 4ᴇ-7
27 ×
28 PI
29 ×
30 RCL× "µr"
31 1/X
32 RCL× "rho"
33 SQRT
34 RCL- "delta"

35 END
