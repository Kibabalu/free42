@ SKIN: Skin effect computations
@
@ µr relative permeability
@ f frequency in Hz
@ rho = spec. resistivity in Ohm*m
@ δelta skin depth in m
@
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@
@ 2021-04-15: Initial version
@ 2021-04-16: Final version
@
00 { 159-Byte Prgm }
01 LBL "SKIN"
02 MVAR "µr"
03 MVAR "f"
04 MVAR "rho"
05 MVAR "delta"
06 VARMENU "SKIN"

07 CF 21 @ continue program execution after a VIEW instruction
08 STOP
09 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
10 PGMSLV ".SKIN"       @ specify solver routine
11 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
12 VIEW IND "CONTROL"   @ View the solution
13 GTO "SKIN"           @ return to start

14 LBL ".SKIN"          @ function to solve
15 XEQ 00 @ call subroutine for default values
16 PI
17 RCL× "f"
18 4ᴇ-7
19 ×
20 PI
21 ×
22 RCL× "µr"
23 1/X
24 RCL× "rho"
25 SQRT
26 RCL- "delta"
27 RTN
28 LBL 00 @ subroutine for default values
29 RCL "µr"
30 X≤0?
31 0.999991 @ default for copper
32 STO "µr"
33 RCL "rho"
34 X≤0?
35 1.678ᴇ-8 @ default for copper
36 STO "rho"
37 RTN
38 END
