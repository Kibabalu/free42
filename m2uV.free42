@ m2uV: dBm to dBuV Converter and vice versa
@
@ converting dBm to dBuV
@
@ dBuv = dBm + 90 + 20 * log(sqrt(Z0))
@
@ Frank Kirschbaum, frank.kirschbaum@gmail.com
@ 2021-04-10: Initial version
@ 2021-04-16: Final version
@
00 { 127-Byte Prgm }
01 LBL "m2uV"
02 MVAR "dBm"
03 MVAR "dBuV"
04 MVAR "Z0"
05 VARMENU "m2uV"
06 CF 21 @ continue programex ecution after a VIEW instruction
07 STOP
08 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
09 PGMSLV ".m2uV"       @ specify solver routine
10 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
11 VIEW IND "CONTROL"   @ View the solution
12 GTO "m2uV"           @ return to start

13 LBL ".m2uV"          @ function to solve
14 XEQ 00 @ call subroutine for default values
15 RCL "Z0"
16 SQRT
17 LOG
18 20
19 ×
20 90
21 +
22 RCL "dBm"
23 +
24 RCL- "dBuV"
25 RTN
26 LBL 00 @ subroutine for default values
27 RCL "Z0"
28 X≤0?
29 50 @ default for Z0
30 STO "Z0"
31 RTN

32 END
