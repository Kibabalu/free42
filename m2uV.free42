@ m2uV: dBm to dBuV Converter and vice versa
@
@ converting dBm to dBuV
@
@ dBuv = dBm + 90 + 20 * log(sqrt(Z0))
@
@ Frank Kirschbaum, frank.kirschbaum@gmail.com
@ 2021-04-10: Initial version
@ 2021-04-16: Final version
@
00 { 127-Byte Prgm }
01 LBL "m2uV"
02 MVAR "dBm"
03 MVAR "dBuV"
04 MVAR "Z0"
05 VARMENU "m2uV"

06 CF 21                @ continue programex ecution after a VIEW instruction
07 REALRES              @ calculate real results only
08 STOP
09 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
10 PGMSLV ".m2uV"       @ specify solver routine
11 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
12 VIEW IND "CONTROL"   @ View the solution
13 GTO "m2uV"           @ return to start

14 LBL ".m2uV"          @ function to solve
15 XEQ 00               @ call subroutine for default values
16 RCL "Z0"
17 SQRT
18 LOG
19 20
20 ×
21 90
22 +
23 RCL "dBm"
24 +
25 RCL- "dBuV"
26 RTN
27 LBL 00               @ subroutine for default values
28 RCL "Z0"
29 X≤0?
30 50                   @ default for Z0
31 STO "Z0"
32 RTN

33 END
