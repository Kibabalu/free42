@ RC: Computing step response of an RC low pass
@
@ Vt instantanious voltage at time t in [V]
@ V1 initial Voltage at the beginning of the step in [V]
@ V2 Voltage after an infinite time in [V]
@ R Resistance in [Ohm]
@ C Capacitance in [F].
@ t Time after step in [s].
@
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@
@ 2021-04-10: Initial version
@ 2021-04-13: bug fixed
@
00 { 112-Byte Prgm }
01 LBL "RC"
02 MVAR "V1"
03 MVAR "V2"
04 MVAR "Vt"
05 MVAR "R"
06 MVAR "C"
07 MVAR "t"
08 VARMENU "RC"

09 CF 21 @ continue programex ecution after a VIEW instruction
10 REALRES @ calculate real results only
11 STOP
12 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
13 PGMSLV ".RC"         @ specify solver routine
14 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
15 VIEW IND "CONTROL"   @ View the solution
16 GTO "RC"             @ return to start

17 LBL ".RC"            @ function to solve
18 RCL "t"
19 RCL÷ "R"
20 RCL÷ "C"
21 +/-
22 E↑X
23 +/-
24 1
25 +
26 RCL "V2"
27 RCL- "V1"
28 ×
29 RCL "V1"
30 +
31 RCL- "Vt"

32 END
