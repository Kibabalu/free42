@ RC: Computing step response of an RC low pass
@
@ Vt voltage at time t in V
@ V1 initial Voltage at the beginning of the step at time 0s in V
@ V2 Voltage after an infinite time in V
@ R Resistance in Ohm
@ C Capacitance in F
@ t Time after step in s
@
@ Frank Kirschbaum, frank.kirschbaumr@gmail.com
@
@ 2021-04-10: Initial version
@ 2021-04-13: bug fixed
@
00 { 110-Byte Prgm }
01 LBL "RC"
02 MVAR "V1"
03 MVAR "V2"
04 MVAR "Vt"
05 MVAR "R"
06 MVAR "C"
07 MVAR "t"
08 VARMENU "RC"

09 CF 21 @ continue program execution after a VIEW instruction
10 STOP
11 ASTO "CONTROL"       @ store the name of the unknown variable in CONTROL
12 PGMSLV ".RC"         @ specify solver routine
13 SOLVE IND "CONTROL"  @ Indirectly specify the variable to be solved
14 VIEW IND "CONTROL"   @ View the solution
15 GTO "RC"             @ return to start

16 LBL ".RC"            @ function to solve
17 RCL "t"
18 RCL÷ "R"
19 RCL÷ "C"
20 +/-
21 E↑X
22 +/-
23 1
24 +
25 RCL "V2"
26 RCL- "V1"
27 ×
28 RCL "V1"
29 +
30 RCL- "Vt"

31 END
